let { split_by_chars } = require("string")
let { eachBlock } = require("%sqstd/datablock.nut")
let { isString, isArray, isTable, isFunction } = require("%sqStdLibs/helpers/u.nut")
let time = require("%scripts/time.nut")
let contentPreview = require("%scripts/customization/contentPreview.nut")
let shopSearchCore = require("%scripts/shop/shopSearchCore.nut")
let stdMath = require("%sqstd/math.nut")
let { targetPlatform, canSpendRealMoney } = require("%scripts/clientState/platform.nut")
let { getLastWeapon, isWeaponEnabled,
  isWeaponVisible } = require("%scripts/weaponry/weaponryInfo.nut")
let { unitClassType, getUnitClassTypeByExpClass } = require("%scripts/unit/unitClassType.nut")
let unitTypes = require("%scripts/unit/unitTypesList.nut")
let { GUI } = require("%scripts/utils/configs.nut")
let { getDefaultPresetId } = require("%scripts/weaponry/weaponryPresets.nut")
let { initUnitWeapons, initWeaponryUpgrades, initUnitModifications, initUnitWeaponsContainers
} = require("%scripts/unit/initUnitWeapons.nut")
let { getWeaponryCustomPresets } = require("%scripts/unit/unitWeaponryCustomPresets.nut")

let MOD_TIERS_COUNT = 4

let unitIntParams = [ "costGold", "rank", "reqExp",
  "repairCost", "repairTimeHrsArcade", "repairTimeHrsHistorical", "repairTimeHrsSimulation",
  "train2Cost", "train3Cost_gold", "train3Cost_exp",
  "gunnersCount", "bulletsIconParam"
]

local Unit = class
{
   name = ""
   rank = 0
   shopCountry = ""
   isInited = false //is inited by wpCost, warpoints, and unitTags

   expClass = unitClassType.UNKNOWN
   unitType = unitTypes.INVALID
   esUnitType = ::ES_UNIT_TYPE_INVALID
   isPkgDev = false

   cost = 0
   costGold = 0
   reqExp = 0
   expMul = 1.0
   gift = null //""
   giftParam = null //""
   event = null //""
   premPackAir = false
   repairCost = 0
   repairTimeHrsArcade = 0
   repairTimeHrsHistorical = 0
   repairTimeHrsSimulation = 0
   freeRepairs = 0
   trainCost = 0
   train2Cost = 0
   train3Cost_gold = 0
   train3Cost_exp = 0
   gunnersCount = 0
   hasDepthCharge = false

   isInShop = false
   reqAir = null //name of unit required by shop tree
   futureReqAir = null
   group = null //name of units group in shop
   fakeReqUnits = null //[] or null when no required fake units
   showOnlyWhenBought = false
   showOnlyWhenResearch = false
   showOnlyIfPlayerHasUnlock = null //"" or null
   hideForLangs = null //[] or null when no lang restrictions
   reqFeature = null //"" or null
   hideFeature = null //"" or null
   reqUnlock = null //"" or null

   customImage = null //""
   customClassIco = null //""
   customTooltipImage = null //""

   tags = null //[]
   weapons = null //[]
   modifications = null //[]
   skins = null //[]
   skinsBlocks = null //{}
   previewSkinId = null //""
   weaponUpgrades = null //[]
   spare = null //{} or null
   needBuyToOpenNextInTier = null //[]

   commonWeaponImage = "#ui/gameuiskin#weapon.png"
   primaryBullets = null //{}
   secondaryBullets = null //{}
   bulletsIconParam = 0

   shop = null //{} - unit params table for shop unit info
   info = null //{} - tank params info

   testFlight = ""

   isToStringForDebug = true

   //!!FIX ME: params below are still set from outside of unit
   modificatorsRequestTime = -1
   modificators = null //{} or null
   modificatorsBase = null //{} or null
   minChars = null //{} or null
   maxChars = null //{} or null
   primaryWeaponMods = null //[] or null
   secondaryWeaponMods = null //{} or null
   bulGroups = -1
   bulModsGroups = -1
   bulletsSets = null //{}
   primaryBulletsInfo = null //[] or null
   shopReq = true //used in shop, but look like shop can get it from other sources than from unit.
   researchType = null
   marketplaceItemdefId = null

   defaultWeaponPreset  = null
   disableFlyout        = false
   hideBrForVehicle     = false
   showShortestUnitInfo = false // only name, tag and image
   nvdSights = null // {[modName]: []} NVD sights for mods info
   hasWeaponSlots = false
   weaponsContainers = null

  //unit table generated by native function gather_and_build_aircrafts_list
  constructor(unitTbl)
  {
    //!!FIX ME: Is it really required? we can init units by unittags self without native code
    foreach(key, value in unitTbl)
      if (key in this)
        this[key] = value

    foreach(key in ["tags", "weapons", "modifications", "skins", "weaponUpgrades", "needBuyToOpenNextInTier"])
      if (!isArray(this[key]))
        this[key] = []
    foreach(key in ["shop", "info", "primaryBullets", "secondaryBullets", "bulletsSets", "skinsBlocks", "weaponsContainers"])
      if (!isTable(this[key]))
        this[key] = {}
  }

  function setFromUnit(unit)
  {
    foreach(key, value in unit)
      if (!isFunction(value)
        && (key in this)
        && !isFunction(this[key])
      )
        this[key] = value
    return this
  }

  function initOnce()
  {
    if (isInited)
      return null
    isInited = true

    let warpoints = ::get_warpoints_blk()
    let uWpCost = getUnitWpCostBlk()

    let expClassStr = uWpCost?.unitClass
    expClass = getUnitClassTypeByExpClass(expClassStr)
    esUnitType = expClass.unitTypeCode
    unitType = unitTypes.getByEsUnitType(esUnitType)

    foreach(p in unitIntParams)
      this[p] = uWpCost?[p] ?? 0

    cost                      = uWpCost?.value || 0
    freeRepairs               = uWpCost?.freeRepairs ?? warpoints?.freeRepairs ?? 0
    expMul                    = uWpCost?.expMul ?? 1.0
    shopCountry               = uWpCost?.country ?? ""
    trainCost                 = uWpCost?.trainCost ?? warpoints?.trainCostByRank[$"rank{rank}"] ?? 0
    gift                      = uWpCost?.gift
    giftParam                 = uWpCost?.giftParam
    event                     = uWpCost?.event
    premPackAir               = uWpCost?.premPackAir ?? false
    hasDepthCharge            = uWpCost?.hasDepthCharge ?? false
    commonWeaponImage         = uWpCost?.commonWeaponImage ?? commonWeaponImage
    customClassIco            = uWpCost?.customClassIco
    customTooltipImage        = uWpCost?.customTooltipImage
    isPkgDev                  = ::is_dev_version && (uWpCost?.pkgDev ?? false)
    researchType              = uWpCost?.researchType
    hideBrForVehicle          = tags.contains("hideBrForVehicle")
    showShortestUnitInfo      = tags.contains("showShortestUnitInfo")
    hasWeaponSlots            = uWpCost?.hasWeaponSlots ?? false

    foreach (weapon in weapons)
      weapon.type <- ::g_weaponry_types.WEAPON.type

    if (uWpCost?.weapons != null) {
      if (hasWeaponSlots)
        initUnitWeaponsContainers(weaponsContainers, uWpCost.weapons)
      initUnitWeapons(this, weapons, uWpCost.weapons)
      initWeaponryUpgrades(this, uWpCost)
    }

    let errorsTextArray = initUnitModifications(modifications, uWpCost?.modifications, esUnitType)
    if (uWpCost?.spare != null)
    {
      spare = {
        name = "spare"
        type = ::g_weaponry_types.SPARE.type
        cost = uWpCost?.spare.value || 0
        image = ::get_weapon_image(esUnitType, ::get_modifications_blk()?.modifications.spare, uWpCost?.spare)
      }
      if (uWpCost?.spare.costGold != null)
        spare.costGold <- uWpCost.spare.costGold
    }

    for(local i = 1; i <= MOD_TIERS_COUNT; i++)
      needBuyToOpenNextInTier.append(uWpCost?[$"needBuyToOpenNextInTier{i}"] ?? 0)

    customImage = uWpCost?.customImage ?? ::get_unit_preset_img(name)
    if (!customImage && ::is_tencent_unit_image_reqired(this))
      customImage = ::get_tomoe_unit_icon(name)
    if (customImage && !::isInArray(customImage.slice(0, 1), ["#", "!"]))
      customImage = ::get_unit_icon_by_unit(this, customImage)
    shopSearchCore.cacheUnitSearchTokens(this)

    return errorsTextArray
  }

  function hasPlatformFromBlkStr(blk, fieldName, defValue = false, separator = "; ")
  {
    let listStr = blk?[fieldName]
    if (!isString(listStr))
      return defValue
    return split_by_chars(listStr, separator).contains(targetPlatform)
  }

  function applyShopBlk(shopUnitBlk, prevShopUnitName, unitGroupName = null)
  {
    isInShop = true
    reqAir = prevShopUnitName
    futureReqAir = shopUnitBlk?.futureReqAir
    group = unitGroupName
    if ("fakeReqUnitType" in shopUnitBlk)
      fakeReqUnits = shopUnitBlk % "fakeReqUnitType"

    let isVisibleUnbought = !shopUnitBlk?.showOnlyWhenBought
      && hasPlatformFromBlkStr(shopUnitBlk, "showByPlatform", true)
      && !hasPlatformFromBlkStr(shopUnitBlk, "hideByPlatform", false)

    showOnlyWhenBought = !isVisibleUnbought
    showOnlyWhenResearch = shopUnitBlk?.showOnlyWhenResearch ?? false

    if (isVisibleUnbought && isString(shopUnitBlk?.hideForLangs))
      hideForLangs = split_by_chars(shopUnitBlk?.hideForLangs, "; ")

    foreach(key in ["reqFeature", "hideFeature", "showOnlyIfPlayerHasUnlock", "reqUnlock"])
      if ((shopUnitBlk?[key] ?? "") != "")
        this[key] = shopUnitBlk[key]

    gift = shopUnitBlk?.gift //we already got it from wpCost. is we still need it here?
    giftParam = shopUnitBlk?.giftParam
    marketplaceItemdefId = shopUnitBlk?.marketplaceItemdefId
    disableFlyout = shopUnitBlk?.disableFlyout ?? false
  }

  isAir                 = @() esUnitType == ::ES_UNIT_TYPE_AIRCRAFT
  isTank                = @() esUnitType == ::ES_UNIT_TYPE_TANK
  isShip                = @() esUnitType == ::ES_UNIT_TYPE_SHIP
  isBoat                = @() esUnitType == ::ES_UNIT_TYPE_BOAT
  isShipOrBoat          = @() esUnitType == ::ES_UNIT_TYPE_SHIP || esUnitType == ::ES_UNIT_TYPE_BOAT
  isSubmarine           = @() esUnitType == ::ES_UNIT_TYPE_SHIP && tags.indexof("submarine") != null
  isHelicopter          = @() esUnitType == ::ES_UNIT_TYPE_HELICOPTER
  //



  getUnitWpCostBlk      = @() ::get_wpcost_blk()?[name]
  isBought              = @() ::shop_is_aircraft_purchased(name)
  isUsable              = @() ::shop_is_player_has_unit(name)
  isRented              = @() ::shop_is_unit_rented(name)
  isBroken              = @() ::isUnitBroken(this)
  isResearched          = @() ::isUnitResearched(this)
  isInResearch          = @() ::isUnitInResearch(this)
  getRentTimeleft       = @() ::rented_units_get_expired_time_sec(name)
  getRepairCost         = @() ::Cost(::wp_get_repair_cost(name))
  getCrewTotalCount     = @() getUnitWpCostBlk()?.crewTotalCount || 1
  getCrewUnitType       = @() unitType.crewUnitType
  getExp                = @() ::getUnitExp(this)

  _endRecentlyReleasedTime = null
  function getEndRecentlyReleasedTime() {
    if (_endRecentlyReleasedTime != null)
      return _endRecentlyReleasedTime

    _endRecentlyReleasedTime = -1
    let releaseDate = ::get_unittags_blk()?[name].releaseDate
    if (releaseDate == null)
      return _endRecentlyReleasedTime

    let recentlyReleasedUnitsDays = GUI.get()?.markRecentlyReleasedUnitsDays ?? 0
    if (recentlyReleasedUnitsDays == 0)
      return _endRecentlyReleasedTime

    _endRecentlyReleasedTime = time.getTimestampFromStringUtc(releaseDate)
      + time.daysToSeconds(recentlyReleasedUnitsDays)
    return _endRecentlyReleasedTime
  }

  _isRecentlyReleased = null
  function isRecentlyReleased()
  {
    if (_isRecentlyReleased != null)
      return _isRecentlyReleased

    _isRecentlyReleased = getEndRecentlyReleasedTime() > ::get_charserver_time_sec()
    return _isRecentlyReleased
  }

  _operatorCountry = null
  function getOperatorCountry()
  {
    if (_operatorCountry)
      return _operatorCountry
    local res = ::get_unittags_blk()?[name].operatorCountry ?? ""
    _operatorCountry = res != "" && ::get_country_icon(res) != "" ? res : shopCountry
    return _operatorCountry
  }

  function getEconomicRank(ediff)
  {
    return ::get_unit_blk_economic_rank_by_mode(getUnitWpCostBlk(), ediff)
  }

  function getBattleRating(ediff)
  {
    if (!::CAN_USE_EDIFF)
      ediff = ediff % EDIFF_SHIFT
    let mrank = getEconomicRank(ediff)
    return ::calc_battle_rating_from_rank(mrank)
  }

  function getWpRewardMulList(difficulty = ::g_difficulty.ARCADE)
  {
    let warpoints = ::get_warpoints_blk()
    let uWpCost = getUnitWpCostBlk()
    let mode = difficulty.getEgdName()

    let isSpecial = ::isUnitSpecial(this)
    let premPart = isSpecial ? warpoints?.rewardMulVisual?.premRewardMulVisualPart ?? 0.5 : 0.0
    let mul = (uWpCost?["rewardMul" + mode] ?? 1.0) *
      (warpoints?.rewardMulVisual?["rewardMulVisual" + mode] ?? 1.0)
    let timedAward = uWpCost?[$"timedAward{mode}"] ?? 0

    return {
      wpMul   = stdMath.round_by_value(mul * (1.0 - premPart), isSpecial ? 0.05 : 0.1)
      wpTimed = timedAward * (1.0 - premPart)
      premMul = stdMath.round_by_value(1.0 / (1.0 - premPart), 0.1)
    }
  }

  function _tostring()
  {
    return "Unit( " + name + " )"
  }

  function canAssignToCrew(country)
  {
    return ::getUnitCountry(this) == country && canUseByPlayer()
  }

  function canUseByPlayer()
  {
    return isUsable() && isVisibleInShop() && unitType.isAvailable()
  }

  function isVisibleInShop()
  {
    if (!isInShop || !unitType.isVisibleInShop())
      return false
    if (::is_debug_mode_enabled || isUsable())
      return true
    if (showOnlyWhenBought)
      return false
    if (hideForLangs && hideForLangs.indexof(::g_language.getLanguageName()) != null)
      return false
    if (showOnlyIfPlayerHasUnlock && !::is_unlocked_scripted(-1, showOnlyIfPlayerHasUnlock))
      return false
    if (showOnlyWhenResearch && !isInResearch() && getExp() <= 0)
      return false
    if (hideFeature != null && ::has_feature(hideFeature))
      return false
    if (::isUnitGift(this) && !canSpendRealMoney())
      return false
    return true
  }

  /*************************************************************************************************/
  /************************************PRIVATE FUNCTIONS *******************************************/
  /*************************************************************************************************/

  function resetSkins()
  {
    skins = []
    skinsBlocks = {}
  }

  function getSkins()
  {
    if (skins.len() == 0)
      skins = ::get_skins_for_unit(name) //always returns at least one entry
    return skins
  }

  function getSkinBlockById(skinId)
  {
    if (!skinsBlocks.len()) //Will be default skin at least.
      foreach (skin in getSkins())
        skinsBlocks[skin.name] <- skin

    return skinsBlocks?[skinId]
  }

  function getPreviewSkinId()
  {
    if (!previewSkinId)
    {
      previewSkinId = ""
      foreach (skin in getSkins())
        if (::g_decorator.getDecorator(name + "/" + skin.name, ::g_decorator_type.SKINS)?.blk?.useByDefault)
          previewSkinId = skin.name
    }
    return previewSkinId
  }

  getSpawnScore = @(weaponName = null) ::shop_get_spawn_score(name, weaponName || getLastWeapon(name), [])

  function getMinimumSpawnScore()
  {
    local res = -1
    foreach (weapon in getWeapons())
      if (isWeaponVisible(this, weapon) && isWeaponEnabled(this, weapon))
      {
        let spawnScore = getSpawnScore(weapon.name)
        if (res < 0 || res > spawnScore)
          res = spawnScore
      }
    return max(res, 0)
  }

  function invalidateModificators()
  {
    if (modificatorsRequestTime > 0)
    {
      ::remove_calculate_modification_effect_jobs()
      modificatorsRequestTime = -1
    }
    modificators = null
  }

  function canPreview()
  {
    return isInShop
  }

  function doPreview()
  {
    if (canPreview())
      contentPreview.showUnitSkin(name)
  }

  isDepthChargeAvailable = @() hasDepthCharge || shop_is_modification_enabled(name, "ship_depth_charge")

  function getNVDSights(modName) {
    if (!isTank())
      return []

    initNVDSightsOnce()
    return nvdSights?[modName] ?? []
  }

  function initNVDSightsOnce() {
    if (nvdSights)
      return

    nvdSights = {}
    eachBlock(::get_full_unit_blk(name)?.modifications, function(mode, modeName) {
      nvdSights[modeName] <- []
      eachBlock(mode?.effects.nightVision, @(_, name) nvdSights[modeName].append(name), this)
    }, this)
  }

  function getDefaultWeapon() {
    if (defaultWeaponPreset)
      return defaultWeaponPreset

    let unitBlk = ::get_full_unit_blk(name)
    if (!unitBlk)
      return null

    defaultWeaponPreset = getDefaultPresetId(unitBlk)
    return defaultWeaponPreset
  }

  function getEntitlements()
  {
    if (gift == null)
      return []

    return ::OnlineShopModel.searchEntitlementsByUnit(name)
  }

  function getUnlockImage()
  {
    if (isAir())
      return "#ui/gameuiskin#blueprint_items_aircraft.png"
    if (isTank())
      return "#ui/gameuiskin#blueprint_items_tank.png"
    if (isShipOrBoat())
      return "#ui/gameuiskin#blueprint_items_ship.png"

    return "#ui/gameuiskin#blueprint_items_aircraft.png"
  }

  isSquadronVehicle       = @() researchType == "clanVehicle"
  getOpenCost             = @() ::Cost(0, ::clan_get_unit_open_cost_gold(name))
  getWeapons = function() {
    if (!hasWeaponSlots || !::has_feature("WeaponryCustomPresets"))
      return weapons

    return [].extend(weapons, getWeaponryCustomPresets(this))
  }
}

::u.registerClass("Unit", Unit, @(u1, u2) u1.name == u2.name, @(unit) !unit.name.len())

return Unit